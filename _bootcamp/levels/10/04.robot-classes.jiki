// -------------------------------------------------
class Robot do
  public property name
  public property age

  constructor with name, age do
    set this.name to name
    set this.age to age
  end

  public method say with string do
    vibrate_air(this.name, string)
  end
end

// -------------------------------------------------
class FormalConversation do
  private property participants
  private property hellos
  private property goodbyes

  constructor with robot1, robot2 do
    set this.participants to [robot1, robot2]
    set this.hellos       to ["Hello ⚡☂♞✿☯.", "Hello ✦☀♻❄☘."]
    set this.goodbyes     to ["Goodbye ★⚔♠✧❀.", "Goodbye ♜⚙❖☾✺."]
  end

  private method exchange_sayings with sayings do
    for each participant in this.participants indexed by i do
      participant.say(sayings[i])
    end
  end

  public method exchange_salutations do
    this.exchange_sayings(this.hellos)
  end

  public method exchange_valedictions do
    this.exchange_sayings(this.goodbyes)
  end

  public method get_participant_name with index do
    set name to "�"
    if index >= 1 and index <= my#length(this.participants) do
      change name to this.participants[index].name
    end
    return name
  end
end

// -------------------------------------------------
// locked
function hold_conversation with name_1, age_1, name_2, age_2, name_3, age_3 do
  set r1 to new Robot(name_1, age_1)
  set r2 to new Robot(name_2, age_2)
  set conversation_1 to new FormalConversation(r1, r2)

  conversation_1.exchange_salutations()
  r1.say(concatenate("I am ", my#number_to_string(r1.age), " years old. How old are you?"))
  r2.say(concatenate("I am ", my#number_to_string(r2.age), ". Together we are ", my#number_to_string(r1.age + r2.age), " years old. Wow."))
  r1.say("Wow.")
  conversation_1.exchange_valedictions()

  set r3 to new Robot(name_3, age_3)
  set conversation_2 to new FormalConversation(r3, r1)
  conversation_2.exchange_salutations()
  r3.say("Who was that?")
  r1.say(concatenate("That was ", conversation_1.get_participant_name(2), "."))
  conversation_2.exchange_valedictions()
end
