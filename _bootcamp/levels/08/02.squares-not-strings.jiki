function turn_around do
  turn_right()
  turn_right()
end

function can_move_in_direction with direction do
  set square to look(direction)
  set no_go to (square.is_wall or square.contents is "ðŸ”¥" or square.contents is "ðŸ’©")
  return square.in_maze and not no_go
end

function can_turn_left do
  return can_move_in_direction("left")
end

function can_turn_right do
  return can_move_in_direction("right")
end

function can_move do
  return can_move_in_direction("ahead")
end

function collect_treasure with treasure_chest do
  set emoji to look("down").contents
  if not my#is_empty(emoji) do
    change treasure_chest to my#dict_incr(treasure_chest, emoji)
    remove_emoji()
  end
  return treasure_chest
end

// --------------------------------------
set emojis to {}

repeat_until_game_over do
  if can_turn_left() do
    turn_left()
    move()
  else if can_move() do
    move()
  else if can_turn_right() do
    turn_right()
    move()
  else do
    turn_around()
  end

  change emojis to collect_treasure(emojis)
end

announce_emojis(emojis)

