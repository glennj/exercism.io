function translate_rna with rna do
  set amino_acids to {
    "AUG": "Methionine",   "UUU": "Phenylalanine",
    "UUA": "Leucine",      "UUC": "Phenylalanine",
    "UUG": "Leucine",      "UGG": "Tryptophan",   
    "UCU": "Serine",       "UGC": "Cysteine",   
    "UCC": "Serine",       "UGU": "Cysteine",
    "UCA": "Serine",
    "UCG": "Serine",       "UAA": "STOP",
    "UAU": "Tyrosine",     "UGA": "STOP",
    "UAC": "Tyrosine",     "UAG": "STOP"
  }

  set codon to ""
  set amino_acid  to ""
  set protein to []

  repeat_forever do
    if is_empty(rna) do
      return protein
    end

    change codon to take(rna, 3)
    change amino_acid to amino_acids[codon]

    if amino_acid is "STOP" do
      return protein
    end

    change protein to push(protein, amino_acid )
    change rna to drop(rna, 3)
  end
end

// ------------------------------------------------------------
function is_empty with string do
  for each _ in string do
    return false
  end
  return true
end

// Grab the first n chars from the string.
// If n is greater than the string length, returns a copy of the string.
function take with string, n do
  set taken to ""

  for each char in string indexed by i do
    change taken to join(taken, char)
    if i == n do
      return taken
    end
  end

  return taken
end

// Remove the first n chars from the string.
// If n is greater than the string length, returns an empty string.
function drop with string, n do
  set remaining to ""

  for each char in string indexed by i do
    if i > n do
      change remaining to join(remaining, char)
    end
  end

  return remaining
end
