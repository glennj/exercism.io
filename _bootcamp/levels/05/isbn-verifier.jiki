function to_digits with isbn do
  set digits to []
  set digit to 0
  set position to 1

  for each char in isbn do
    if char is "-" do
      next // valid separator
    else if my#is_digit(char) do
      change digit to string_to_number(char)
    else if position == 10 and char == "X" do
      change digit to 10
    else do
      return [] // invalid character
    end
    change digits to push(digits, digit)
    change position to position + 1
  end

  return digits
end

function isbn_sum with digits do
  set sum to 0
  for each digit in digits indexed by i do
    change sum to sum + digit * (11 - i)
  end
  return sum
end

function is_valid_isbn with isbn do
  set digits to to_digits(isbn)
  if my#length(digits) != 10 do
    return false
  end
  set sum to isbn_sum(digits)
  return sum % 11 == 0
end
