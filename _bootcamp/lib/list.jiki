// Jiki-supplied functions used:
//  * push(aList, anElement)

// ------------------------------------------------------------
// String or list functions

function length with thing do
  set len to 0
  for each _ in thing do
    change len to len + 1
  end
  return len
end

function is_empty with string do
  for each _ in string do
    return false
  end
  return true
end

// ------------------------------------------------------------
// Return the index of the *first* occurrence of the given 
// (scalar only) element in the given list.
// Return -1 if the element does not exist in the list.
function index_of with haystack, needle do
  for each elem in haystack indexed by index do
    if elem is needle do        // scalars only!
      return index
    end
  end
  return -1
end

// Return true if the given (scalar only) element is in the given list.
function contains with haystack, needle do
  return index_of(haystack, needle) != -1
end

// Return a copy of the list with the element at the given index removed.
function remove_at with list, idx do
  set result to []

  for each elem in list indexed by i do
    if i != idx do
      change result to push(result, list[i])
    end
  end

  return result
end

// Reverse a list
function reverse with list do
  set reversed to []
  set len to length(list)
  set i to len + 1

  repeat i times do
    change i to i - 1
    change reversed to push(reversed, list[i])
  end

  return reversed
end

