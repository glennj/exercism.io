function my#split with string, separator do
  /// Splits a string into words using a separator character.
  //  @param {string} string - The string to split.
  //  @param {string} separator - The separator must be a single character.
  //  @returns {list<string>}
  //  @requires concatenate()
  //  @requires push()
  //  @see my#is_empty
  //
  set words to []
  set word to ""

  for each char in string do
    if not (char is separator) do
      change word to concatenate(word, char)

    else do
      if not my#is_empty(word) do
        change words to push(words, word)
      end
      change word to ""
    end
  end

  if not my#is_empty(word) do
    change words to push(words, word)
  end

  return words
end
