\ I haven't seen a truly boolean regex matching word
: matches? \ s r -- T
  r:match    \ s r -- r n
  0 n:= not
  nip
  ;

: question? \ s -- T
  /[?]$/ matches? ;

: yelling? \ s -- T
  dup  /[[:alpha:]]/ matches?
  swap /[[:lower:]]/ matches? not
  and ;

\ we have already trimmed whitespace
: silent? \ s -- T
  s:len 0 n:= 
  ;
  
\ Bob responds to input with an output
: bob \ s -- s
  \ trim trailing whitespace
  /\s+$/ "" s:replace!

  silent? if drop "Fine. Be that way!" ;; then

  dup question?
  swap yelling?
  2dup and      \ yelled-question?

  if 2drop "Calm down, I know what I'm doing!" ;; then  \ a yelled question
  if drop  "Whoa, chill out!"                  ;; then  \ yelling
  if       "Sure."                             ;; then  \ question
           "Whatever."
  ;
